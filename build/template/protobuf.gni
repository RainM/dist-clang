template("protobuf") {
  assert(defined(invoker.sources), "Need |sources| for protobuf")

  action_name = "${target_name}_gen"
  source_set_name = target_name
  action_foreach(action_name) {
    visibility = [ ":$source_set_name" ]
    script = "//build/protoc.py"
    sources = invoker.sources
    outputs = [
      "{{source_gen_dir}}/{{source_name_part}}.pb.cc",
      "{{source_gen_dir}}/{{source_name_part}}.pb.h",
      "{{source_gen_dir}}/{{source_name_part}}_pb2.py",
    ]
    args = [
      rebase_path("//src", root_build_dir),
      rebase_path("$root_gen_dir/src", root_build_dir),
      "{{source}}",
    ]
    deps = [
      "//src/third_party/protobuf:protoc",
    ]
  }

  target_type = "source_set"
  if (defined(invoker.shared) && invoker.shared) {
    target_type = "shared_library"
  }

  target(target_type, target_name) {
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    if (defined(invoker.visibility)) {
      visibility += invoker.visibility
    }

    visibility += [ "//:*" ]

    sources = get_target_outputs(":$action_name")

    if (!config_for_tests) {
      configs -= [ "//build/config:no_rtti" ]
    }

    public_deps = [
      ":$action_name",
      "//src/third_party/protobuf:protobuf",
    ]
  }
}
