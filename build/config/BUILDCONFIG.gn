# =============================================================================
# Command-line arguments, with default values.
# =============================================================================

declare_args() {
  # Build for debugging.
  config_for_debug = false

  # Build for testing.
  config_for_tests = false

  # Build version.
  version = exec_script("//build/version.py", [], "value")

  # Use Clang ASan.
  use_asan = false

  # Use Clang TSan.
  use_tsan = false
}

assert(!(use_asan && use_tsan), "Use either ASan or TSan at the same time")

# =============================================================================
# Setup configurations.
# =============================================================================

default_configs = [
  "//build/config:asan",
  "//build/config:base",
  "//build/config:no_stdlibs",
  "//build/config:tsan",
]

if (config_for_tests) {
  if (!use_tsan) {
    use_asan = true
    if (os == "mac") {
      # FIXME: ASan binaries doesn't run on Mac
      use_asan = false
    }
  }

  default_configs += [
    "//build/config:debug",
  ]
}
else {
  default_configs += [
    "//build/config:no_exceptions",
    "//build/config:no_rtti",
  ]

  if (config_for_debug) {
    default_configs += [ "//build/config:debug" ]
  } else {
    default_configs += [ "//build/config:release" ]
  }
}

if (os == "linux") {
  default_configs += [ "//build/config/linux:base" ]
}
if (os == "mac") {
  default_configs += [ "//build/config/mac:base" ]
  default_configs -= [ "//build/config:no_stdlibs" ]
}

# =============================================================================
# Setup dependencies.
# =============================================================================

default_deps = []
default_deps += [
  "//src/third_party/libcxx:c++",
  "//src/third_party/libcxxabi:c++abi",
]

# Mac OS X 10.10 doesn't work with tcmalloc anymore.
if (os != "mac") {
  default_deps += [ "//src/third_party/gperftools:tcmalloc" ]
}

# =============================================================================
# Set defaults.
# =============================================================================

set_defaults("executable") {
  configs = default_configs
  deps = default_deps
}

set_defaults("shared_library") {
  configs = default_configs
  deps = default_deps
}

set_defaults("source_set") {
  configs = default_configs
  deps = default_deps
}

set_defaults("static_library") {
  configs = default_configs
  deps = default_deps
}

# =============================================================================
# Filter platform-specific sources.
# =============================================================================

linux_sources_filters = [
  "*_linux.h",
  "*_linux.cc",
  "*_linux_test.h",
  "*_linux_test.cc",
]

mac_sources_filters = [
  "*_mac.h",
  "*_mac.cc",
  "*_mac_test.h",
  "*_mac_test.cc",
]

sources_assignment_filter = []
if (os != "mac") {
  sources_assignment_filter += mac_sources_filters
}
if (os != "linux") {
  sources_assignment_filter += linux_sources_filters
}

set_sources_assignment_filter(sources_assignment_filter)

# =============================================================================
# Setup toolchain.
# =============================================================================

if (os == "linux") {
  set_default_toolchain("//build/toolchain/linux:clang")
}
if (os == "mac") {
  set_default_toolchain("//build/toolchain/mac:clang")
}
