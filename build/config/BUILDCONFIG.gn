# =============================================================================
# Command-line arguments, with default values.
# =============================================================================

declare_args() {
  # Build with support for Google Performance Tools (gperftools).
  use_profiler = false

  # Link with the tcmalloc library.
  use_tcmalloc = false

  # Build for debugging.
  config_for_debug = false

  # Build for testing.
  config_for_tests = false

  # Build version.
  version = exec_script("//build/version.py", [], "value")
}

# =============================================================================
# Setup configurations.
# =============================================================================

default_configs = [
  "//build/config:base",
  "//build/config:no_stdlibs",
]

if (config_for_tests) {
  default_configs += [
    "//build/config:debug",
  ]
}
else {
  default_configs += [
    "//build/config:no_exceptions",
    "//build/config:no_rtti",
  ]

  if (config_for_debug) {
    default_configs += [ "//build/config:debug" ]
  } else {
    default_configs += [ "//build/config:release" ]
  }
}

if (os == "linux") {
  default_configs += [ "//build/config/linux:base" ]
}
if (os == "mac") {
  default_configs += [ "//build/config/mac:base" ]
  default_configs -= [ "//build/config:no_stdlibs" ]
}

if (use_profiler) {
  default_configs += [ "//build/config:profiler" ]
}

if (use_tcmalloc) {
  default_configs += [ "//build/config:tcmalloc" ]
}

# =============================================================================
# Setup dependencies.
# =============================================================================

default_deps = []

if (os == "linux") {
  default_deps += [ "//src/third_party/libcxx:c++" ]
}

# =============================================================================
# Set defaults.
# =============================================================================

set_defaults("executable") {
  configs = default_configs
  if (config_for_tests) {
    configs += [ "//build/config:asan" ]
  }

  deps = default_deps
}

set_defaults("shared_library") {
  configs = default_configs
  deps = default_deps
}

set_defaults("source_set") {
  configs = default_configs
  deps = default_deps
}

set_defaults("static_library") {
  configs = default_configs
  deps = []
}

# =============================================================================
# Filter platform-specific sources.
# =============================================================================

linux_sources_filters = [
  "*_linux.h",
  "*_linux.cc",
  "*_linux_test.h",
  "*_linux_test.cc",
]

mac_sources_filters = [
  "*_mac.h",
  "*_mac.cc",
  "*_mac_test.h",
  "*_mac_test.cc",
]

sources_assignment_filter = []
if (os != "mac") {
  sources_assignment_filter += mac_sources_filters
}
if (os != "linux") {
  sources_assignment_filter += linux_sources_filters
}

set_sources_assignment_filter(sources_assignment_filter)

# =============================================================================
# Setup toolchain.
# =============================================================================

if (os == "linux") {
  set_default_toolchain("//build/toolchain/linux:clang")
}
if (os == "mac") {
  set_default_toolchain("//build/toolchain/mac:clang")
}
