import("//build/template/protobuf.gni")

executable("clang") {
  visibility = [ "//:All" ]

  sources = [
    "clang_main.cc",
  ]

  deps += [
    ":client",
    ":config_proto",
    "//src/base:base",
    "//src/base:logging",
    "//src/third_party/protobuf:protobuf",
  ]
}

# Separate source set for unit-tests.
source_set("client") {
  visibility = [
    ":*",
    "//src/test:unit_tests",
  ]

  sources = [
    "clang.cc",
    "clang.h",
  ]

  deps += [
    ":command",
    "//src/base:base",
    "//src/base:logging",
    "//src/net:net",
  ]
}

# NOTICE: Uses custom libclang ldflags, thus, have to be shared library.
shared_library("command") {
  visibility = [
    ":*",
    "//src/test:unit_tests",
  ]

  sources = [
    "command.cc",
    "command.h",
  ]

  configs += [ "//build/config:libclang" ]

  deps += [
    "//src/base:base",
    "//src/base:logging",
  ]

  public_configs = [ "//build/config:libclang_includes" ]
}

protobuf("config_proto") {
  sources = [
    "configuration.proto",
  ]
}
