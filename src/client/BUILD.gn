executable("clang") {
  visibility = [ "//:All" ]

  sources = [ "clang_main.cc" ]

  deps += [
    ":client",
    ":configuration",
    "//src/base:base",
    "//src/base:logging",
    "//src/third_party/protobuf:protobuf",
  ]
}

source_set("client") {
  visibility = [
    ":*",
    "//src/test:unit_tests",
  ]

  sources = [
    "clang.cc",
    "clang.h",
  ]

  deps += [
    ":command",
    "//src/base:base",
    "//src/base:logging",
    "//src/net:net",
    "//src/proto:proto",
    "//src/third_party/protobuf:protobuf",
  ]
}

# NOTICE: Uses custom libclang ldflags, thus, have to be shared library.
shared_library("command") {
  visibility = [
    ":*",
    "//src/test:unit_tests",
  ]

  sources = [
    "command.cc",
    "command.h",
  ]

  configs += [ "//build/config:libclang" ]

  deps += [
    "//src/base:base",
    "//src/base:logging",
    "//src/proto:proto",
    "//src/third_party/protobuf:protobuf",
  ]

  public_configs = [ "//build/config:libclang_includes" ]
}

shared_library("configuration") {
  output_name = "conf_client"

  visibility = [ ":*" ]

  sources = [
    "configuration.pb.cc",
    "configuration.pb.h",
  ]

  if (!config_for_tests) {
    configs -= [ "//build/config:no_rtti" ]
  }

  deps += [ "//src/third_party/protobuf:protobuf" ]
}
